plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
	id 'io.github.juuxel.loom-quiltflower' version '1.8.+'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group


loom {
	accessWidenerPath = file("src/main/resources/brewinandchewin.accesswidener")
	runs {
		datagen {
			server()

			name "Minecraft Data"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"

			runDir "build/datagen"
		}
	}
}

sourceSets.main.resources.srcDirs += ['src/main/generated']

repositories {
	maven {
		name = "Greenhouse Maven"
		url = 'https://maven.greenhouseteam.dev/releases/'
	}
	maven { url = "https://mvn.devos.one/snapshots/" } // Porting Lib
	maven {
		url "https://maven.jamieswhiteshirt.com/libs-release" // Reach Entity Attributes (Required by Porting Lib)
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}
	maven {
		name "Cloth Config, REI"
		url "https://maven.shedaniel.me"
	}
	maven {
		name = "Jitpack, ASM, MixinExtras"
		url  = "https://jitpack.io"
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
	}
	maven {
		name = "EMI"
		url  = "https://maven.terraformersmc.com/"
	}
	maven {
		name = "Curse Forge"
		url = "https://www.cursemaven.com"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	//mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"


	modApi "dev.architectury:architectury-fabric:9.1.10"
	modApi("me.shedaniel.cloth:cloth-config-fabric:11.1.106") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modImplementation("vectorwing:FarmersDelight:${fdrf_version}") {
		exclude(group: "net.fabricmc")
	}
	modImplementation "curse.maven:roughly-enough-items-310111:${project.rei_version}"
	//modImplementation "curse.maven:roughly-enough-resources-325625:${project.rer_version}"

	//Fabric ASM
	modImplementation include ("com.github.Chocohead:Fabric-ASM:v2.3")

	//Mixin Extras
	include(implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:0.2.0-beta.9")))

	//EMI
	modImplementation "dev.emi:emi-fabric:${project.emi_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
	}
}
